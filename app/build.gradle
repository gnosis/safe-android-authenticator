apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'bivrost'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "io.gnosis.safe.authenticator"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode getInt("APP_VERSION_CODE", 2)
        versionName getKey("APP_VERSION_NAME", "0.1.0 - Praecox")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField javaTypes.LONG, "BLOCKCHAIN_CHAIN_ID", getKey("BLOCKCHAIN_CHAIN_ID", "4")
        buildConfigField javaTypes.STRING, "BLOCK_EXPLORER_TX", asString(getKey("BLOCK_EXPLORER_TX", "https://rinkeby.etherscan.io/tx/%s"))
        buildConfigField javaTypes.STRING, "INFURA_API_KEY", asString(getKey("INFURA_API_KEY", ""))
        buildConfigField javaTypes.STRING, "BLOCKCHAIN_NET_URL", asString(getKey("BLOCKCHAIN_NET_URL", "https://rinkeby.infura.io/v3/"))
        buildConfigField javaTypes.STRING, "RELAY_SERVICE_URL", asString(getKey("RELAY_SERVICE_URL", "https://safe-relay.rinkeby.gnosis.pm/api/"))
        buildConfigField javaTypes.STRING, "TRANSACTION_SERVICE_URL", asString(getKey("TRANSACTION_SERVICE_URL", "https://safe-transaction.rinkeby.gnosis.io/api/"))
        buildConfigField javaTypes.STRING, "INSTANT_TRANSFER_SERVICE_URL", asString(getKey("INSTANT_TRANSFER_SERVICE_URL", "https://safe-rinkeby-relay.herokuapp.com/api/"))

        buildConfigField javaTypes.STRING, "ENS_REGISTRY", asString(getKey("ENS_REGISTRY", "0x00000000000C2E074eC69A0dFb2997BA6C7d2e1e"))
        buildConfigField javaTypes.STRING, "ALLOWANCE_MODULE", asString(getKey("ALLOWANCE_MODULE", "0xb4f7291fA058fbcbdcBb347647459aCb2f714a80"))
        buildConfigField javaTypes.STRING, "MULTI_SEND_ADDRESS", asString(getKey("MULTI_SEND_ADDRESS", "0x8d29be29923b68abfdd21e541b9374737b49cdad"))

        vectorDrawables.useSupportLibrary = true
        project.ext.set("archivesBaseName", String.format("safe-authenticator-%s", defaultConfig.versionCode))

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        compileOptions {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    signingConfigs {
        debug {
            // We add the debug keystore to repo so that the travis build will always have the same signature
            storeFile file("../debug.keystore")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            firebaseAppDistribution {
                releaseNotes=getKey("APP_RELEASE_NOTES")
                groups=getKey("FIREBASE_GROUP")
            }
        }
        mainnet {
            debuggable true
            applicationIdSuffix ".mainnet"
            versionNameSuffix "-mainnet"
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField javaTypes.LONG, "BLOCKCHAIN_CHAIN_ID", getKey("BLOCKCHAIN_CHAIN_ID", "1")
            buildConfigField javaTypes.STRING, "BLOCK_EXPLORER_TX", asString(getKey("BLOCK_EXPLORER_TX", "https://etherscan.io/tx/%s"))
            buildConfigField javaTypes.STRING, "BLOCKCHAIN_NET_URL", asString(getKey("BLOCKCHAIN_NET_URL", "https://mainnet.infura.io/v3/"))
            buildConfigField javaTypes.STRING, "RELAY_SERVICE_URL", asString(getKey("RELAY_SERVICE_URL", "https://safe-relay.gnosis.io/api/"))
            buildConfigField javaTypes.STRING, "TRANSACTION_SERVICE_URL", asString(getKey("TRANSACTION_SERVICE_URL", "https://safe-transaction.gnosis.io/api/"))
            buildConfigField javaTypes.STRING, "INSTANT_TRANSFER_SERVICE_URL", asString(getKey("INSTANT_TRANSFER_SERVICE_URL", "https://safe-mainnet-relay.herokuapp.com/api/"))

            buildConfigField javaTypes.STRING, "ALLOWANCE_MODULE", asString(getKey("ALLOWANCE_MODULE", "0x1DB1a2063504D15D36e9506A192ab7BBb17dbb2A"))

            firebaseAppDistribution {
                releaseNotes=getKey("APP_RELEASE_NOTES")
                groups=getKey("FIREBASE_GROUP")
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation "androidx.appcompat:appcompat:${versions.androidx_app_compat}"
    implementation "androidx.core:core-ktx:${versions.androidx_core_ktx}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.androidx_constraint_layout}"
    implementation "androidx.recyclerview:recyclerview:$versions.androidx_recycler_view"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:${versions.androidx_swipe_refresh}"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.androidx_lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.androidx_lifecycle"

    kapt "androidx.room:room-compiler:$versions.androidx_room"
    implementation "androidx.room:room-runtime:$versions.androidx_room"
    implementation "androidx.room:room-ktx:$versions.androidx_room"

    implementation "com.google.android.material:material:$versions.material"

    implementation 'com.jpardogo.materialtabstrip:library:1.1.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinx_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinx_coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:$versions.kotlinx_coroutines"

    implementation "com.github.gnosis.svalinn-kotlin:blockies:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:crypto:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:models:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:utils:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:android-common:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:security:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:mnemonic:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:mnemonic-android:$versions.svalinn"

    implementation "com.github.gnosis.bivrost-kotlin:bivrost-solidity-types:$versions.bivrost"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation("com.squareup.retrofit2:converter-moshi:$versions.retrofit") {
        exclude group: "com.squareup.moshi", module: "moshi"
    }

    implementation "com.squareup.picasso:picasso:$versions.picasso"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // Wallet Connect
    implementation "com.github.WalletConnect:kotlin-walletconnect-lib:$versions.wallet_connect"

    // Koin for Android
    implementation "org.koin:koin-android:$versions.koin"
    // Koin Android Scope features
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    // Koin Android ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    // Koin Android Experimental features
    implementation "org.koin:koin-androidx-ext:$versions.koin"

    implementation "com.squareup.moshi:moshi:$versions.moshi"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshi"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation "com.google.zxing:core:$versions.zxing"

    implementation "com.crashlytics.sdk.android:crashlytics:$versions.crashlytics"
    implementation 'com.google.firebase:firebase-perf:19.0.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
}

apply plugin: 'com.google.gms.google-services'
